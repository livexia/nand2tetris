// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zeroing or negating x
    Mux16(a=x, b=false, sel=zx, out=x0);
    Not16(in=x0, out=notx0);
    Mux16(a=x0, b=notx0, sel=nx, out=x1);

    // zeroing or negating y
    Mux16(a=y, b=false, sel=zy, out=y0);
    Not16(in=y0, out=noty0);
    Mux16(a=y0, b=noty0, sel=ny, out=y1);

    // elect between and-ing and addition
    Add16(a=x1, b=y1, out=addf);
    And16(a=x1, b=y1, out=andf);
    Mux16(a=andf, b=addf, sel=f, out=f0);

    // negating z
    // with two's complement msb equal to ng, also split output for zr
    Not16(in=f0, out=notf0);
    Mux16(a=f0, b=notf0, sel=no, out=out, out[15..15]=ng, out[0..7]=o0, out[8..15]=o1);
    
    // Or16(a=f1, out[0..7]=o0, out[8..15]=o1);
    Or8Way(in=o0, out=zr0);
    Or8Way(in=o1, out=zr1);
    Or(a=zr0, b=zr1, out=nzr);
    Not(in=nzr, out=zr);

    // Or16(a=f1, out[15..15]=s);
    // Or(a=s, out=ng);
}